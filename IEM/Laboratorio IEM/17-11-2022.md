## Scrivere sulla canvas
si usa test()
Ci sono 2 variabili:
- PIFont per conservare i font
- PiImage per conservare immagini
``` js

PFont f; //variabile che conterrà il mio font

void setup(){
  size(500,500);
  f = laodFont("Calibri",16);
  // println(PFont.list()); vedo i font nel pc
  textFont(f);
  
}

void draw(){
  background(255);
  fill(0);
  textSize(50);
  text("Ciao World bla1 bla2 bla3 bla4",0,height/2+25,width,height/2);
}
```
si possono usare i font presenti gia nel pc.
creiamo il suo font
f = createFont("Calibri",16); crea il font calibri a dimensione 16

  println(PFont.list()); per vedere a console i font installati
il punto x e y (origine) è il punto in basso a sinistra.

text("testo", posizione x, posizione y, max canvasX,max canvas Y).  Gli ultimi due parametri servono per dire quando deve troncare il testo.

voglio creare il font di star wars che da piu grande 

creo un font da strumenti e me lo salva nella cartella data.
textAlign() allinea il testo all'interno del text_box dove si scrive il testo

``` js
PFont f; //variabile che conterrà il mio font
float s=90; //size testo
int y; //usata per far muovere la scritta

void setup(){
  size(500,500);
  f = loadFont("Star-Jedi-50.vlw");
  // println(PFont.list()); vedo i font nel pc
  textFont(f);
  textAlign(CENTER,TOP);
  y=height-5;
}

void draw(){
  background(0);
  fill(255,255,0);
  textSize(s);
  text("Star trek",0,y,width,height/2);
  
  s-=0.5;
  y-=3;
}
```
prendo in input il nome dell'utente:

``` js
PFont f; //variabile che conterrà il mio font
float s=90; //size testo
int y; //usata per far muovere la scritta
String nome = "";
void setup(){
  size(500,500);
  f = loadFont("Star-Jedi-50.vlw");
  // println(PFont.list()); vedo i font nel pc
  textFont(f);
  textAlign(CENTER,TOP);
  y=height-5;
}

void draw(){
  background(0);
  fill(255,255,0);
  textSize(40);
  text(nome,100,20);
  textSize(s);
  text("Star trek",0,y,width,height/2);
  
  s-=0.5;
  y-=3;
}

void keyTyped(){
  nome+=key;
}
```
Implemento la cancellazione mentre scrivo:

``` js
PFont f; //variabile che conterrà il mio font
float s=90; //size testo
int y; //usata per far muovere la scritta
String nome = "";
void setup(){
  size(500,500);
  f = loadFont("Star-Jedi-50.vlw");
  // println(PFont.list()); vedo i font nel pc
  textFont(f);
  textAlign(CENTER,TOP);
  y=height-5;
}

void draw(){
  background(0);
  fill(255,255,0);
  textSize(40);
  text(nome,100,20);
  textSize(s);
  text("Star trek",0,y,width,height/2);
  
  if(s>2){
    s-=0.5;
  }
  y-=3;
}

void keyTyped(){
  if(key==BACKSPACE){
    nome=nome.substring(0,nome.length()-1);
  }
  else{
    nome+=key;
  }
}
```
---
simulo l'accelerazione di gravità facendo rimbalzare quadrato

```js
float x;
float y = 20;
float s = 0;
float g = 0.1; //accelerazione

void setup(){
  size(500,300);
  x=width/2;
  rectMode(CENTER);
}

void draw(){
  background(0);
  rect(x,y,20,20);
  y+=s;
  s+=g; //aggiungo accelerazione
  
  if(y>height){
    s*=-0.95;
    y=height;
  }
  println(s); //stampo la velocità
}
```
---
ho un rettangolo al centro e lo voglio ruotare a dx o sinistra.

uso rotate(). funziona:
- ruota sull'origine del sistema di riferimento, cioè in base alla canvas
- si associa la traslazione. devo traslare 
Tutte le trasformazioni vengono messe in delle matrici, si possono separare le matrici per far ruotare solo una matrice solo su un oggetto----> si fa con pushMatrix() e PopMatrix()


```int a=0; //angolo
void setup(){
  size(500,500);
  rectMode(CENTER);
}

void draw(){
  background(0);
  
  pushMatrix();//figura che ruota
    translate(width/2,height/2);
    rotate(radians(a));
    fill(255);
    rect(0,0,50,50);
  popMatrix(); //fine figura che ruota
  
  //fill(255,255,0);
  //rect(width/2+50,height/2,50,50);
  a-=3;
}
```

creo moto di rotazione e rivoluzione (come se fosse la luna)

```
int a=0; //angolo
void setup(){
  size(500,500);
  rectMode(CENTER);
}

void draw(){
  background(0);
  
  translate(width/2,height/2);
  rotate(radians(a/2));
  pushMatrix();//figura che ruota
    translate(100,0);
    rotate(radians(a));
    fill(255);
    rect(0,0,50,50);
  popMatrix(); //fine figura che ruota
  
  //fill(255,255,0);
  //rect(width/2+50,height/2,50,50);
  a-=3;
}
```
---
vediamo altri tipi di operazioni affini
Aumentiamo lo spessore del bordo + area del rettangolo, e quindi non singolarmente come si è fatto finora

```float s=1; //scale
void setup(){
  size(500,500);
  rectMode(CENTER);
}

void draw(){
  background(255);
  translate(width/2,height/2);
  scale(s);
  rect(0,0,50,50);
  s+=0.01;;
}
```

al posto di scale posso usare:
![[Pasted image 20221117115611.png]]
o anche shearY(s);

esse funzionano con i translate
queste funzioni lavorano su tutta la canvas.
```
float s=1; //scale
void setup(){
  size(500,500);
  rectMode(CENTER);
}

void draw(){
  background(255);
  translate(width/2,height/2);
  scale(s,s-2);
  rect(0,0,50,50);
  s+=0.01;

}
```
---
Programmazione a oggetti
clicco e ho una pallina. clicco e ho un'altra pallina ecc...
conservo le palline in un ArrayList per evitare di avere un array con dimensione fissata.

in java si può usare tipo generico. quindi posso usare la classe super e posso anche usare le classi che ereditano da essa.

risolvere problema che quando la palla arriva in alto si blocca (con tante ball)

Vedere il file completo nella cartella "classi"


``` classi
ArrayList <Ball> bs; //array di Ball

void setup(){
  size(800,500);
  background(0); // faccio anche la scia dopo con un rect grande quanto la canvas
  bs=new ArrayList<Ball>();
}


void draw(){
  noStroke();
  fill(0,30);
  rect(0,0,width,height); //per la scia
  
  for (Ball b:bs){ //per ogni elemento b in bs
    b.run();
  }
  
}


void keyPressed(){
  if(key=='b' || key == 'B'){
    bs.add(new Ball(random(30,width-30),random(30,height-30),random(20,60),random(1,10),random(1,10)));
  }
  if(key=='g' || key == 'G'){
    bs.add(new GreenBall(random(30,width-30),random(30,height-30),random(20,60),random(1,10),random(1,10)));
  }
  
  if(key=='r' || key == 'R'){
    setup(); //reset della canvas
  }
}
```

``` ball
class Ball{
  float px, py, size, sx, sy; //px=posx py=posy sy=speed y
  
  Ball(float x, float y, float size, float sx, float sy){
  this.px=x;
  this.py=y;
  this.size=size;
  this.sx=sx;
  this.sy=sy;
  }
  
  
  Ball(float x, float y, float size){
    this(x,y,size,0,0); //0 0 valori di default  
  }


  void display(){ //disegna la figura
    fill(255,255,0);
    noStroke();
    ellipse(this.px, this.py, this.size, this.size);
  }
  
  
  void move(){
    px+=sx;
    py+=sy;
  }

  
  void bounce(){
    if(px<=size/2 || px>=width-size/2){
      sx=-sx;
    }
    if(py<this.size/2 || py>height-this.size/2){
      sy=-sy;
    }
  }

  void gravity(){
    sy+=0.1;
  }

  
  void run(){
    this.gravity();
    this.move();
    this.bounce();
    this.display();
  }
}
```


```greenball
class GreenBall extends Ball{
  
  GreenBall(float x, float y, float s, float sx, float sy){
    super(x,y,s,sx,sy); //chiamo costruttore madre
  }
  
  GreenBall(float x, float y, float size){
    super(x,y,size);
  }
  
  void display(){
    fill(0,255,0);
    stroke(128,0,255);
    strokeWeight(5);
    ellipse(px,py,size,size);
  }
  
}
```
