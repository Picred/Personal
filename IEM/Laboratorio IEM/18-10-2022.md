`rect()` con il **quinto parametro** si possono smussare gli angoli del rettangolo.
>per esempio `rect(x,y,w,h,10)` 
>10 sono i pixel che conta dall'origine verso x e y e smussa quel punto

`rect(100,100,200,150,10);`
quella a 8 parametri, inizia dall'angolo a sinistra in senso orario per smussare gli angoli singolarmente -> `rect(100,100,200,200,10,20,30,40);`
>Stiamo usando processing in maniera statica ed esso produce una mappa di pixel raster.

La **versione interattiva** permette di modificare l'output nel tempo.
# <font color="red">CANVAS DINAMICA</font>
Funzione draw() è un loop infinito. Associata sempre a setup().
- `print()` stampa semplicemente
- `println()` stampa e va a capo (ln=new line)

``` processing
void setup(){
  println("Sono nel setup)!");

}

void draw(){
  println("Sono nel draw!");
}
```
Il **FRAMERATE** di base è **60 fps** ed esso si può cambiare con `frameRate(20);` cambio gli fps a 20.

- Il tempo si misura con quanti fps sono stati ***renderizzati*** dall'inizio dell'animazione con `frameCount` che è la variabile che conta gli fps passati.
``` Processing
void setup(){
  println("Sono nel setup)!");
  frameRate(1);

}

void draw(){
  println("Sono nel draw! Sono passati " +frameCount+ " frame");
}
```
>se per esempio volessi lo sfondo cambiato di colore dopo 2 secondi, dovrei scrivere una cosa del tipo:
```
if(frameCount >= 120) {...}
```

## <font color ="blue">Quanti millisecondi sono passati dall'inizio?</font>

uso `millis()/1000` per trovare i **secondi**.
- La divisione è fra interi. Il risultato è con la virgola e si viene presa la parte intera e basta;
- per questo motivo si fa un casting esplicito:
![[Pasted image 20221018102743.png]]
```  
println("Sono nel draw! Sono passati " +(float)millis()/1000+ " frame");

```
# <font color="red">ANIMAZIONE</font>
**Effetto pacman**
Un vettore può essere scomposto in 2 componenti:
`x+=v*dir` -> cioè posso scomporre il vettore **x** in $velocità * direzione$
`dir=+1` verso destra (in caso di x)
`dir=-1` verso sinistra (in caso di x)
Analogamente se utilizzo in caso di y.

Quindi **cambio direzione** con `dir*=-1;`
```
int x=0;
int v=3;
int dir=1;
void setup(){
  size(500,500);
  strokeWeight(4);

}

void draw(){
  background(255);
  line(x,0,x,height);
  
  if(x<0 || x>=width){
    dir=-dir;
  }
  
  x+=v*dir;
}
```

Codice completo
```
int x=0;
int v=3;
int dir=1;
int c= 255;

void setup(){
  size(500,500);
  strokeWeight(4);
  background(c);
  
}

void draw(){
  stroke(random(255),random(255),random(255));
  line(x,0,x,height);
  
  if(x<0 || x>=width){
    dir=-dir;
  }
  c--;
  x+=v*dir;
}
```


# <font color="red">PACMAN</font>
Per disegnare PACMAN devo utilizzare `arc();`
Funziona come `ellipse()` ma ci sono altri 2 valori:
- **inizio** disegno cerchio
- **fine** disegno cerchio.
L'angolo 0 parte da **(width,0)** come nel *piano cartesiano* e la rotazione è in senso <font color="orange"><b>ORARIO</b></font>.
Gli angoli sono in radianti e abbiamo a disposizione:
<q><font color =#FF00FF><b>PI, HALF_PI,
 QUARTER_PI, TWO_PI</b></font></q>
- La funzione `radians(90)` restituisce l'angolo, che è scritto in gradi, in radianti
...quindi converte l'angolo da gradi a radianti.
```
size(500,500);
   background(0);
   fill(255,255,0);
   arc(width/2,height/2,100,100,PI/4,TWO_PI-PI/4);
   
```
## <font color="blue">Codice PACMAN</font>
``` Processing
int diametro = 150; //diametro pacman
int a = 45; //angolo bocca
int v = 4; //velocita pacman e bocca
int x=diametro/2; //asse x
int d=1; //direzione pacman
int dira = 1; //direzione dell'angolo

void setup(){
  size(1000,300);
  fill(#FFFF00);
}

void draw(){
  background(0);
  if(d>0){
    arc(x,height/2,diametro,diametro,radians(a),radians(360-a));
  }
  else {
    arc(x,height/2,diametro,diametro,radians(180+a),radians(360+180-a));
  }
  
  if(x<75 || x>width-75){
    d=-d;
  }
  
  if(a < 0 || a>=45){
    dira=-dira;
  }
  
  x+=v*d;
  a+=v*dira; 
}
```
---
# <font color="red">Shape() per le figure con più vertici</font>
>Le coordinate di un punto le possiamo prendere con **Paint** puntando il cursore sull'immagine e leggendole in basso a sinistra. Per esempio:
>(83,393)
>(196,435)
>(186,195)

Disegno una freccia:
```
beginShape();
  
   vertex(180,82);
   vertex(207,36);
   vertex(214,63);
   vertex(407,11);
   
   vertex(412,30);
   vertex(219,82);
   vertex(226,109);
   
  
  endShape(CLOSE); //chiude la figura, evito di inserire l'ultimo vertice
```

## <font color="blue">Freccia che si muove </font>
```
int x=0;

void setup(){
  size(500,500);

}

void draw(){
  background(0);
  beginShape();
  fill(255,0,0);
  stroke(255);
   vertex(x+180,82);
   vertex(x+207,36);
   vertex(x+214,63);
   vertex(x+407,11);
   
   vertex(x+412,30);
   vertex(x+219,82);
   vertex(x+226,109);
   
  
  endShape(CLOSE);
  
  x++;
}

```
`mouseX` e `mouseY` sono le coordinate del mouse sulla canvas
# <font color="red">CURVA DI BEIZER</font>
Ha i seguenti attributi (non in ordine):
- primo punto , **punto di ancoraggio** A_1, 
- punto di ancoraggio A_2 è anche nominato il **punto di fine**
- **punti di controllo** ed essi sono sempre 2 

*Sintassi:*
`bezier(anchor_point,control_point,control_point,anchor_point)`

```
void setup(){
  size(500,500);
  noFill();
}

void draw(){
  background(255);
  fill(255,255,0);
  
  //anchor point
  stroke(0,255,0);
  strokeWeight(10);
  point(185,50);
  point(190,300);
  
  //control point
  stroke(255,0,0);
  strokeWeight(10);
  point(mouseX,mouseY);
  point(15,290);
  
  //linee
  stroke(0);
  strokeWeight(1);
  
  line(185,50,mouseX,mouseY);
  line(15,290,190,300);
  
  strokeWeight(2);
  bezier(185,50,mouseX,mouseY,15,290,190,300);
}
```
`dist()`distanza fra 2 punti.
- Come valuto se la freccia sta puntando dentro un cerchio?(per farlo illuminare) Se la posizione del mouse è minore del raggio allora esso è dentro **il cerchio**, altrimenti no.
```
int q = 10; //quantità di cerchi da disegnare
int d; //diametro cerchi
int b=0; //blu

void setup(){
  size(500,500);
  d=width/q;
  colorMode(RGB,width);
  noStroke();
}

void draw(){
  background(width);
  
  for(int y=d/2; y<=height; y+=d){
    for(int x=d/2; x<=width; x+=d){
     if(dist(x,y,mouseX,mouseY)<d/2){
       b=width; 
      }
      else {
        b=0;
      }
      fill(x,y,b);
      ellipse(x,y,d,d);
    }
  }
  
}
```

# <font color="red">TEXTURE</font>
>Proviamo a creare una **texture**

`random(10)` crea un numero random compreso fra \[0.0,9.99999\]
`random(n1,n2)` crea un numero random compreso fra \[n1,n2\]
``` Processing
int q = 30; //quantità di cerchi da disegnare
int d; //diametro cerchi
int b=0; //blu

void setup(){
  size(800,800);
  d=width/q;
  noLoop();
  noStroke();
}

void draw(){
  background(0,255,0);
  
  for(int y=0; y<=height; y+=d){
    for(int x=0; x<=width; x+=d){
     
      fill(random(255),130);
      rect(x+random(-5,5),y+random(-5,5),d,d);
    }
  }
}
```

``` Processing
int q = 30; //quantità di cerchi da disegnare
int d; //diametro cerchi
int b=0; //blu
int r=50; //raggio

void setup(){
  size(800,800);
  d=width/q;
  //noLoop();
  noStroke();
}

void draw(){
  background(0,255,0);
  
  for(int y=0; y<=height; y+=d){
    for(int x=0; x<=width; x+=d){
     
      fill(random(255),130);
      rect(x+random(-5,5),y+random(-5,5),d,d);
    }
  }
  fill(255,0,0);
  ellipse(width/2,height/2,r,r);
  r++;
}
```

``` Processing
// Far rimbalzare il cerchio nei bordi della canvas per esercizio
int q = 30; //quantità di cerchi da disegnare
int d; //diametro cerchi
int b=0; //blu
int r=50; //raggio
int vr = 1; //velocità del vettore raggio
int dirr=1; //"direzione" del vettore raggio

void setup(){
  size(800,800);
  d=width/q;
  //noLoop();
  noStroke();
}

void draw(){
  background(0,255,0);
  
  for(int y=0; y<=height; y+=d){
    for(int x=0; x<=width; x+=d){
     
      fill(random(255),130);
      rect(x+random(-5,5),y+random(-5,5),d,d);
    }
  }
  fill(255,0,0);
  ellipseMode(RADIUS);
  ellipse(width/2,height/2,r,r);
  
  if(r>=width/2 || r<=0){
    dirr*=-1;
  }
  r+=vr*dirr;
}
```